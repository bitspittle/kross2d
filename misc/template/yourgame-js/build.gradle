import java.awt.*

// Script based on
// https://kotlinlang.org/docs/tutorials/javascript/getting-started-gradle/getting-started-with-gradle.html

apply plugin: 'kotlin2js'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile project(":yourgame-common")
}

task assembleWeb(type: Sync) {
    group = 'Build'
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/web/lib"

    dependsOn classes
}

task cleanWeb(type: Delete) {
    group = 'Build'
    delete "${projectDir}/web/lib"
}

task runWeb {
    group = 'Application'
    doLast {
        Desktop.desktop.open file("${projectDir}/web/index.html")
    }
}

assemble.dependsOn assembleWeb
clean.dependsOn cleanWeb
runWeb.dependsOn assembleWeb
